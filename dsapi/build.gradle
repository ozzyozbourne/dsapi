plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
}

group 'io.github.ozzyozbourne'
version '2.0.1'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'com.arakelian:faker:3.10.0'
    implementation 'com.google.api-client:google-api-client:2.0.1'
    implementation 'com.google.oauth-client:google-oauth-client-jetty:1.34.1'
    implementation 'com.google.apis:google-api-services-sheets:v4-rev20220927-2.0.0'
    implementation 'com.google.auth:google-auth-library-oauth2-http:1.13.0'
}

tasks.named('test') {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications{
        mavenJava(MavenPublication){

            groupId = 'io.github.ozzyozbourne'
            artifactId = 'dsapi'
            version = '2.0.1'
            from components.java

            pom {
                name = 'dsapi'
                description = 'Google sheets CRUD with ease'
                url = 'https://github.com/ozzyozbourne/dsapi'
                inceptionYear = '2022'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'khanosaid'
                        name = 'osaid khan'
                        email = 'khanosaid726@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git:github.com/ozzyozbourne/dsapi.git'
                    developerConnection = 'scm:git:ssh://github.com/ozzyozbourne/dsapi.git'
                    url = 'https://github.com/ozzyozbourne/dsapi'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = project.storedUsername
                password = project.storedPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

tasks.withType(JavaCompile) {
    options.release = 8
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}